SAMIUTVB ;;ven/lgc - UNIT TEST for SAMIVSTA ; 12/27/18 12:44pm
 ;;18.0;SAMI;;
 ;
 ; VA-PALS will be using Sam Habiel's [KBANSCAU] broker
 ;   to pull information from the VA server into the
 ;   VA-PALS client and, to push TIU notes generated by
 ;   VA-PALS forms onto the VA server.
 ; Using this broker between VistA instances requires
 ;   not only the IP and Port for the server be known,
 ;   but also, that the Access and Verify of the user
 ;   on the server be sent across as well.  This is
 ;   required as the user on the server must have the
 ;   necessary Context menu(s) allowing use of the
 ;   Remote Procedure(s).
 ; Six parameters have been added to the client
 ;   VistA to prevent the necessity of hard coding
 ;   certain values and to allow for default values for others.
 ;   SAMI PORT
 ;   SAMI IP ADDRESS
 ;   SAMI ACCVER
 ;   SAMI DEFAULT PROVIDER
 ;   SAMI DEFAULT STATION NUMBER
 ;   SAMI TIU NOTE PRINT NAME
 ;   SAMI DEFAULT CLINIC IEN
 ;   SAMI SYSTEM TEST PATIENT DFN
 ; Note that the user selected must have active
 ;   credentials on both the Client and Server systems
 ;   and the following Broker context menu.
 ;      OR CPRS GUI CHART
 ;
 ; NOTE: Unit tests will pull data using the local
 ;       client VistA files rather than risk degrading
 ;       large datasets in use.  NEVERTHELESS, it is
 ;       recommended that UNIT TESTS be run when
 ;       VA-PALS is not in use as some Graphstore globals
 ;       are temporarily moved while testing is running.
 ;
 ; @section 0 primary development
 ;
 ; @routine-credits
 ; @primary-dev: Larry Carlson (lgc)
 ;  larry@fiscientific.com
 ; @primary-dev-org: Vista Expertise Network (ven)
 ;  http://vistaexpertise.net
 ; @copyright: 2012/2018, ven, all rights reserved
 ; @license: Apache 2.0
 ;  https://www.apache.org/licenses/LICENSE-2.0.html
 ;
 ; @application: SAMI
 ; @version: 18.0
 ; @patch-list: none yet
 ;
 ; @to-do
 ;
 ; @section 1 code
 ;
START I $T(^%ut)="" W !,"*** UNIT TEST NOT INSTALLED ***" Q
 D EN^%ut($T(+0),2)
 Q
 ;
STARTUP ; Set up dfn and tiuien to use throughout testing
 ;s utdfn="dfn"_$J
 s utdfn=$$GET^XPAR("SYS","SAMI SYSTEM TEST PATIENT DFN",,"Q")
 s (utsuccess,tiuien)=0
 ; Set up graphstore graph on test patient
 ;n root s root=$$setroot^%wd("vapals-patients")
 n root s root=$$SETROOT^SAMIUTST("vapals-patients")
 k @root@("graph","XXX00001")
 n SAMIUPOO D PLUTARR^SAMIUTST(.SAMIUPOO,"all XXX00001 forms")
 m @root@("graph","XXX00001")=SAMIUPOO
 Q
SHUTDOWN ; ZEXCEPT: dfn,tiuien
 K utdfn,tiuien,utsuccess
 Q
SETUP Q
TEARDOWN Q
 ;
 ;
UTVIT ; @TEST - Pull Vitals on a patient
 ; D VIT(dfn,sdate,edate)
 ; Find entry in patient-lookup without a 'vitals node'
 ;  however the patient has vitals in 120.5
 N D,D0,DG,DI,DIC,DICR,DIG,DIH
 ;N root s root=$$setroot^%wd("patient-lookup")
 n root s root=$$SETROOT^SAMIUTST("patient-lookup")
 N gien s gien=0
 N gnode,utdfn,utNeedsVitUpdate s utNeedsVitUpdate=0
 F  S gien=$O(@root@(gien)) Q:'gien  D  Q:utNeedsVitUpdate
 . S utdfn=$G(@root@(gien,"dfn")) Q:'utdfn
 . I $O(^GMR(120.5,"C",utdfn,0)) D
 .. s gnode=$NA(@root@(gien,"vitals")),gnode=$Q(@gnode)
 .. I '(gnode["vitals") s utNeedsVitUpdate=1
 I 'gien D  Q
 . D FAIL^%ut("Unable to find suitable patient for Vitals - FAILED!")
 ;Found a good patient
 S utsuccess=$$VIT^SAMIVSTB(utdfn)
 s gnode=$NA(@root@(gien,"vitals")),gnode=$Q(@gnode)
 s utsuccess=(gnode["vitals")
 D CHKEQ^%ut(utsuccess,1,"Testing updating Vitals FAILED!")
 Q
 ;
UTVPR ; @TEST - Pull Virtual Patient Record (VPR) on a patient
 ; D VPR(dfn)
 ; Find entry in patient-lookup without an 'inpatient' node'
 N D,D0,DG,DI,DIC,DICR,DIG,DIH
 ;N root s root=$$setroot^%wd("patient-lookup")
 n root s root=$$SETROOT^SAMIUTST("patient-lookup")
 N gien s gien=0
 N gnode,utdfn,utNeedsVPRUpdate s utNeedsVPRUpdate=0
 F  S gien=$O(@root@(gien)) Q:'gien  D  Q:utNeedsVPRUpdate
 . S utdfn=$G(@root@(gien,"dfn")) Q:'utdfn
 . I '$D(@root@(gien,"inpatient")) s utNeedsVPRUpdate=1
 I 'gien D  Q
 . D FAIL^%ut("Unable to find suitable patient for VPR - FAILED!")
 ;Found a good patient
 S utsuccess=$$VPR^SAMIVSTB(utdfn)
 H 1
 s utsuccess=$D(@root@(gien,"inpatient"))
 D CHKEQ^%ut(utsuccess,1,"Testing updating with VPR  FAILED!")
 Q
 ;
 ;
EOR ;End of routine SAMIUTVB
